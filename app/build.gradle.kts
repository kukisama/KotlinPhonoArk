plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("org.jetbrains.kotlin.plugin.compose")
    id("com.google.devtools.ksp")
    id("com.google.dagger.hilt.android")
}

android {
    namespace = "com.phonoark"
    compileSdk = 35

    defaultConfig {
        applicationId = "com.phonoark"
        minSdk = 26
        targetSdk = 35
        versionCode = 1
        versionName = "1.0.0"
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        // CI 通过 -Pabi=arm64-v8a 指定仅构建 arm64；本地默认构建全架构
        val abi = project.findProperty("abi") as? String
        if (abi != null) {
            ndk { abiFilters.clear(); abiFilters.add(abi) }
        }
    }

    buildTypes {
        release {
            isMinifyEnabled = true
            isShrinkResources = true
            signingConfig = signingConfigs.getByName("debug")
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        compose = true
    }
}

// Voice pack extraction: extract US-Jenny WAV files from in-repo zip
val voiceZip = file("src/main/assets/US-Jenny.zip")
val voiceAssetsDir = file("src/main/assets/Exportfile/US-Jenny")

tasks.register<Copy>("extractVoicePack") {
    from(zipTree(voiceZip))
    into(voiceAssetsDir)
    eachFile {
        // Flatten nested root directory if present
        val segments = relativePath.segments
        if (segments.size > 1 && segments[0].equals("US-Jenny", ignoreCase = true)) {
            relativePath = RelativePath(true, *segments.drop(1).toTypedArray())
        }
    }
    includeEmptyDirs = false
    onlyIf { voiceZip.exists() && (!voiceAssetsDir.exists() || voiceAssetsDir.listFiles()?.isEmpty() == true) }
}

tasks.named("preBuild") { dependsOn("extractVoicePack") }

dependencies {
    // Compose BOM
    val composeBom = platform("androidx.compose:compose-bom:2024.12.01")
    implementation(composeBom)
    implementation("androidx.compose.material3:material3")
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.ui:ui-tooling-preview")
    implementation("androidx.compose.material:material-icons-extended")
    debugImplementation("androidx.compose.ui:ui-tooling")

    // AndroidX
    implementation("androidx.core:core-ktx:1.15.0")
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:2.8.7")
    implementation("androidx.lifecycle:lifecycle-viewmodel-compose:2.8.7")
    implementation("androidx.activity:activity-compose:1.9.3")
    implementation("androidx.navigation:navigation-compose:2.8.5")

    // Room
    implementation("androidx.room:room-runtime:2.6.1")
    implementation("androidx.room:room-ktx:2.6.1")
    ksp("androidx.room:room-compiler:2.6.1")

    // Hilt
    implementation("com.google.dagger:hilt-android:2.54")
    ksp("com.google.dagger:hilt-android-compiler:2.54")
    implementation("androidx.hilt:hilt-navigation-compose:1.2.0")

    // Gson
    implementation("com.google.code.gson:gson:2.11.0")

    // Testing
    testImplementation("junit:junit:4.13.2")
    testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test:1.9.0")
    testImplementation("org.mockito:mockito-core:5.14.2")
    testImplementation("org.mockito.kotlin:mockito-kotlin:5.4.0")
}
